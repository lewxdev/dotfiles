#!/bin/zsh
# DEST:~/.zshrc

# Environment constants...

# An associative array of common directories
# accessed like "${DIR[KEY]}"
declare -xrA DIR=(
  # Applications
  [APPS]=/Applications
  [PWAS]=~/Applications/Chrome\ Apps.localized

  # Documents
  [DESK]=~/Desktop
  [DLS]=~/Downloads
  [DOCS]=~/Documents

  # Developer Modules
  [DEV]=~/Developer
  [DOTF]=~/dotfiles
  [SHM]=~/dotfiles/src
  [SHB]=~/dotfiles/src/bin
  [SHS]=~/dotfiles/src/scripts
)

# Paths, etc.
local VSCBIN=${DIR[APPS]}/Visual\ Studio\ Code.app/Contents/Resources/app/bin
export PATH=$PATH:$VSCBIN
export EDITOR="code --wait"
export SSH_AUTH_SOCK=~/.1password/agent.sock

# Conditional setup...
# Allows for multi-device usage where some utilities are not available
# or configured in the same way as other devices

# 1password
if mdfind -name kMDItemFSName=="1Password.app" | grep . &>/dev/null; then
  local OP_DIR=~/.1password
  if ! [[ -d $OP_DIR ]]; then
    mkdir $OP_DIR
    ln -s ~/Library/Group\ Containers/2BUA8C4S2C.com.1password/t/agent.sock $OP_DIR/agent.sock
  fi

  export SSH_AUTH_SOCK=$OP_DIR/agent.sock

  local OP_PLUGINS=~/.config/op/plugins.sh
  if [[ -e $OP_PLUGINS ]]; then
    source $OP_PLUGINS
  fi
fi

# direnv
if which direnv &>/dev/null; then
  eval "$(direnv hook zsh)"

  # we provide our own initial logs
  export DIRENV_LOG_FORMAT=""
fi

# homebrew (latest)
local BREW_EXEC=/opt/homebrew/bin/brew
if [[ -x $BREW_EXEC ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
fi

# oh-my-zsh
local OMZ_DIR=~/.oh-my-zsh
if [[ -d $OMZ_DIR ]]; then
  export ZSH=$OMZ_DIR
  source ${DIR[DOTF]}/oh-my-zsh/preferences.zsh
fi

# pyenv
local PYENV_DIR=~/.pyenv
if [[ -d $PYENV_DIR ]]; then
  export PYENV=$PYENV_DIR
  export PATH=$PYENV/bin:$PATH
  eval "$(pyenv init -)"
  eval "$(pyenv virtualenv-init -)"
fi

# Source customizations...

# Ensure all files in "src/bin" are executable
for bin in $(find ${DIR[SHB]} -type f -not -name "*.*"); do
  if [[ ! -x $bin ]]; then
    chmod +x $bin
  fi
done
export PATH=${DIR[SHB]}:$PATH

# Source all custom functions, aliases, etc.
for module in $(find ${DIR[SHM]} -type f -name "*.zsh"); do
  # Added a conditional to ignore certain files w/ designator
  if [[ ! $(head -2 $module) =~ "^# SOURCE-IGNORE$" ]]; then
    source $module
  fi
done
